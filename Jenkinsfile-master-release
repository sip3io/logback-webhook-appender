#!/usr/bin/env groovy

def releaseVersion
def releaseBranch

pipeline {
    agent any

    tools {
        maven "maven3"
    }

    stages {
        stage('Init') {
            steps {
                configFileProvider([configFile(fileId: '75d7a907-05a7-49c4-8ca4-ecfc124b4afb', targetLocation: 'settings.xml')]) {
                    sh 'mkdir -p ~/.m2 && cp $WORKSPACE/settings.xml -f ~/.m2/settings.xml'
                }
            }
        }

        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Prepare version') {
            steps {
                // Drop -SNAPSHOT qualifier
                sh 'mvn versions:set -DremoveSnapshot'

                // Release version
                script {
                    def pom = readMavenPom file: 'pom.xml'
                    releaseVersion = pom.version
                }
                echo "Release version: ${releaseVersion}"

                // Release branch
                script {
                    releaseBranch = "release/${releaseVersion}"
                }
                echo "Release branch: ${releaseBranch}"

                // Commit version changes
                sh 'mvn versions:commit'
            }
        }

        stage('Build') {
            steps {
                sh 'mvn -DskipTests=true clean package'
                archiveArtifacts artifacts: '**/target/*.jar', fingerprint: true
            }
        }

        stage('Test') {
            steps {
                sh 'mvn test'
            }

            post {
                always {
                    junit 'target/surefire-reports/**/*.xml'
                }
            }
        }

        stage('Branch') {
            steps {
                // Create release branch
                sh "git checkout -b ${releaseBranch}"
                sh "git commit -a -m \"New release ${releaseVersion}\""
                sh "git push https://${GITHUB_TOKEN}:x-oauth-basic@github.com/sip3io/logback-webhook-appender.git ${releaseBranch}"
            }
        }

        stage('Release') {
            steps {
                // Add tag
                sh "git tag ${releaseVersion}"
                sh "git push https://${GITHUB_TOKEN}:x-oauth-basic@github.com/sip3io/logback-webhook-appender.git --tags"

                // Deploy to internal artifactory
                sh 'mvn deploy -DskipTests=true -DaltDeploymentRepository=asip3-releases-ce::default::${SIP3_MAVEN_REPO_URI}/releases-ce'
            }
        }
    }

    post {
        always {
            cleanWs()
        }
    }
}